SELECT DISTINCT ?countryLabel ?capitalLabel ?continentLabel ?currencyLabel ?population WHERE {
  ?country wdt:P31 wd:Q3624078 .
  ?country wdt:P36 ?capital    .
  ?country wdt:P30 ?continent  .
  ?country wdt:P38 ?currency   .

  # not a former country
  FILTER NOT EXISTS { ?country wdt:P31 wd:Q3024240 }
  # and no an ancient civilisation (needed to exclude ancient Egypt)
  FILTER NOT EXISTS { ?country wdt:P31 wd:Q28171280 }

  # remove unused currencies
  FILTER NOT EXISTS { ?currency pq:P582 ?currencyEndDate }

  # get the current population
  ?country p:P1082 [ ps:P1082 ?population; pq:P585 ?date1 ]
  # remove duplicate entries for population
  FILTER NOT EXISTS {
    ?country p:P1082 [ pq:P585 ?date2 ]
    FILTER (?date2 > ?date1)
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
}
ORDER BY ?countryLabel
